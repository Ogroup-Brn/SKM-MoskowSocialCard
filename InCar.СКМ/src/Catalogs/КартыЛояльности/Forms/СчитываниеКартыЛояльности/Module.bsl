

&НаСервере
&Вместо("ОбработатьПолученныйКодНаСервере")
Функция СКМ_ОбработатьПолученныйКодНаСервере(Данные, ТипКодаКарты, Предобработка)
	Перем флСтатус, стрРезультат;
	стрПрефиксСКМ = "964390";
	
	//
	//	Проверка штрихкода на соответствие социальной карте москвича
	//
	Если ТипКодаКарты = Перечисления.ТипыКодовКарт.Штрихкод 
		И СтрДлина(Данные) = 28
		И СтрНайти(Данные, стрПрефиксСКМ) = 1 Тогда
		
		СКМ_ОбщегоНазначенияВызовСервера.ПроверитьКарту(Данные, флСтатус, стрРезультат);
		Если Не ЗначениеЗаполнено(флСтатус) Тогда
			
			//
			//	Ошибка связи с сервером
			//
			ТекстСообщения = "Социальная карта москвича. " + стрРезультат;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,	"ПервыйКодНовойКарты");
			Возврат Ложь;
			
		ИначеЕсли флСтатус Тогда
			
			//
			//	Действующая карта
			//
			ТекстСообщения = "Действующая социальная карта москвича. " + стрРезультат;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,	"ПервыйКодНовойКарты");
			СКМ_ОбщегоНазначенияВызовСервера.СоздатьКартуЛояльностиСКМПоШтрихкоду(Данные);
			
		Иначе 
			
			//
			//	Не действующая карта
			//
			ТекстСообщения = "Социальная карта москвича не действует. " + стрРезультат;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,	"ПервыйКодНовойКарты");
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	//
	//	Выполнение стандартной обработки
	//
	Результат = ПродолжитьВызов(Данные, ТипКодаКарты, Предобработка);
	Возврат Результат;
КонецФункции

