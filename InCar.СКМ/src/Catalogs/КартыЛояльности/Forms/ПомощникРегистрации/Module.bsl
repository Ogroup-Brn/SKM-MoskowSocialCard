

&НаСервере
&Вместо("ОбработатьПервыйКодНовойКартыНаСервере")
Функция СКМ_ОбработатьПервыйКодНовойКартыНаСервере(Данные, ТипКода, Предобработка)
	Перем флСтатус, стрРезультат;
	стрПрефиксСКМ = "964390";
	
	//
	//	Проверка штрихкода на соответствие социальной карте москвича
	//
	Если ТипКода = Перечисления.ТипыКодовКарт.Штрихкод 
		И СтрДлина(Данные) = 28
		И СтрНайти(Данные, стрПрефиксСКМ) = 1 Тогда
		
		СКМ_ОбщегоНазначенияВызовСервера.ПроверитьКарту(Данные, флСтатус, стрРезультат);
		Если Не ЗначениеЗаполнено(флСтатус) Тогда
			
			//
			//	Ошибка связи с сервером
			//
			ТекстСообщения = "Социальная карта москвича. " + стрРезультат;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,	"ПервыйКодНовойКарты");
			Возврат Ложь;
			
		ИначеЕсли флСтатус Тогда
			
			//
			//	Действующая карта
			//
			ТекстСообщения = "Действующая социальная карта москвича. " + стрРезультат;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,	"ПервыйКодНовойКарты");
			Возврат ПродолжитьВызов(Данные, ТипКода, Предобработка);
			
		Иначе 
			
			//
			//	Не действующая карта
			//
			ТекстСообщения = "Социальная карта москвича не действует. " + стрРезультат;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,	"ПервыйКодНовойКарты");
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат ПродолжитьВызов(Данные, ТипКода, Предобработка);
	
КонецФункции
