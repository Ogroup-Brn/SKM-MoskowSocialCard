
&НаСервере
Процедура ПриОткрытииНаСервере()
	флИспользоватьКарты = Константы.ИспользоватьКартыЛояльности.Получить();
	ЭтаФорма.Элементы.СоздаватьКартыЛояльности.Доступность	= флИспользоватьКарты;
	ЭтаФорма.Элементы.ВидКартыЛояльности.Доступность		= Объект.СоздаватьКартыЛояльности;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	индВхождениеДвухКосых 	= СтрНайти(ТекущийОбъект.Адрес, "//");
	стрАдресБезHTTP 		= ?(индВхождениеДвухКосых > 0, Сред(ТекущийОбъект.Адрес, индВхождениеДвухКосых + 2), ТекущийОбъект.Адрес);
	
	индВхождениеДвоеточия	= СтрНайти(стрАдресБезHTTP, ":");
	индВхождениеПервойКосой	= СтрНайти(стрАдресБезHTTP, "/");
	
	Если индВхождениеДвоеточия > 0 И индВхождениеПервойКосой > индВхождениеДвоеточия Тогда
		//
		//	Вычисляем адрес сайта, вычисляем порт, вычисляем сервис
		//
		ТекущийОбъект.АдресСервера = Лев(стрАдресБезHTTP, индВхождениеДвоеточия - 1);
		стрПорт	= Сред(стрАдресБезHTTP, индВхождениеДвоеточия + 1, индВхождениеПервойКосой - индВхождениеДвоеточия - 1);
		Попытка
			ТекущийОбъект.Порт = Число(стрПорт);
		Исключение
			ТекущийОбъект.Порт = 80;
		КонецПопытки;
		ТекущийОбъект.АдресРесурса = Сред(стрАдресБезHTTP, индВхождениеПервойКосой);
	КонецЕсли;
	
	//
	//	При записи активного - очистка активного в остальных наборах
	//
	Если ТекущийОбъект.Активное Тогда
		_Запрос = Новый Запрос;
		_Запрос.Текст = "ВЫБРАТЬ
		                |	СКМ_Настройки.Ссылка КАК Ссылка
		                |ИЗ
		                |	Справочник.СКМ_Настройки КАК СКМ_Настройки
		                |ГДЕ
		                |	СКМ_Настройки.Активное = ИСТИНА
		                |	И СКМ_Настройки.Ссылка <> &Ссылка";
		_Запрос.УстановитьПараметр("Ссылка", ТекущийОбъект.Ссылка);
		_НаборАктивныхЗаписей = _Запрос.Выполнить().Выгрузить();
		Для каждого _Запись Из _НаборАктивныхЗаписей Цикл
			_ОбъектРедактировать = _Запись.Ссылка.ПолучитьОбъект();
			_ОбъектРедактировать.ОбменДанными.Загрузка = Истина;
			_ОбъектРедактировать.Активное = Ложь;
			_ОбъектРедактировать.Записать();
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздаватьКартыЛояльностиПриИзменении(Элемент)
	ЭтаФорма.Элементы.ВидКартыЛояльности.Доступность		= Объект.СоздаватьКартыЛояльности;
КонецПроцедуры
