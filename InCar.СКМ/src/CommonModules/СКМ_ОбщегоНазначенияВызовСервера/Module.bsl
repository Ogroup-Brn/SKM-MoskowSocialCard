&НаСервере
Функция ПроверитьНаличиеОбъектов() Экспорт
	
		//
	//	Наличие вида карты лояльности
	//
	стрИмяКарты				= "Социальная Карта Москвича, все льготники";
	стрКартаКомментарий		= "Карта лояльности по номеру социальной карты москвича";
	стрСтартовыйНомерКарты  = "9643900000000000000 00000000";
	стрКонечныйНомерКарты	= "9643910000000000000 00000000";
	интДлинаШтрихкода		= 28;
	
	ссВидКарты = Справочники.ВидыКартЛояльности.НайтиПоНаименованию(стрИмяКарты, Истина);
	Если ссВидКарты.Пустая() Тогда
		
		ссВидКарты = Справочники.ВидыКартЛояльности.СоздатьЭлемент();
		ссВидКарты.Наименование			= стрИмяКарты;
		ссВидКарты.ДатаНачалаДействия 	= ТекущаяДата();
		ссВидКарты.Комментарий			= стрКартаКомментарий;
		ссВидКарты.Статус				= Перечисления.СтатусыВидовКартЛояльности.Действует;
		ссВидКарты.ТипКарты				= Перечисления.ТипыКарт.Штриховая;
		
		ссШаблонКода = ссВидКарты.ШаблоныКодовКартЛояльности.Добавить();
		ссШаблонКода.ДлинаШтрихкода				= интДлинаШтрихкода;
		ссШаблонКода.КонецДиапазонаШтрихкода    = стрКонечныйНомерКарты;
		ссШаблонКода.НачалоДиапазонаШтрихкода	= стрСтартовыйНомерКарты;
		
		ссВидКарты.Записать();
		
	КонецЕсли;

	//
	//	Наличие наборов настроек "Основной" и "Тестовый"
	//
	стрНаименованиеОсновного	= "Основной";
	стрНаименованиеТестового	= "Тестовый";
	
	стрАдресОсновного			= "http://webservices.soccard.ru:7781/gateway/services/SID0003167";
	стрАдресТестового			= "http://37.230.240.33:7778/gateway/services/SID0003134";
	
	стрРесурсОсновного			= "/gateway/services/SID0003167";
	стрРесурсТестового			= "/gateway/services/SID0003134";
	
	стрСайтОсновного			= "webservices.soccard.ru";
	стрСайтТестового			= "37.230.240.33";
	
	стрПользовательОсновного	= "VOENTORG";
	стрПользовательТестового	= "login";
	
	стрПарольОсновного			= "njiug455";
	стрПарольТестового			= "password1";
	
	стрАкцептантОсновного		= "0008-1855-201902-0000";
	стрАкцептантТестового		= "0001-0123-201702-0000";
	
	интПортОсновного			= 7781;
	интПортТестового			= 7778;
	
	элОсновнаяНастройка = Справочники.СКМ_Настройки.НайтиПоНаименованию(стрНаименованиеОсновного, Истина);
	Если элОсновнаяНастройка.Пустая() Тогда
		//
		//	Создание набора настроек "Основной"
		//
		элОсновнаяНастройка = Справочники.СКМ_Настройки.СоздатьЭлемент();
		элОсновнаяНастройка.Наименование		= стрНаименованиеОсновного;
		элОсновнаяНастройка.Адрес				= стрАдресОсновного;
		элОсновнаяНастройка.Активное			= Истина;
		элОсновнаяНастройка.Пароль				= стрПарольОсновного;
		элОсновнаяНастройка.Логин				= стрПользовательОсновного;
		элОсновнаяНастройка.АдресРесурса		= стрРесурсОсновного;
		элОсновнаяНастройка.АдресСервера		= стрСайтОсновного;
		элОсновнаяНастройка.Порт				= интПортОсновного;
		элОсновнаяНастройка.ПозИд				= стрАкцептантОсновного;
		элОсновнаяНастройка.ВидКартыЛояльности	= ссВидКарты.Ссылка;
		элОсновнаяНастройка.Записать();
	КонецЕсли; 
	
	элТестоваяНастройка = Справочники.СКМ_Настройки.НайтиПоНаименованию(стрНаименованиеТестового, Истина);
	Если элТестоваяНастройка.Пустая() Тогда
		//
		//	Создание набора настроек "Тестовый"
		//
		элТестоваяНастройка = Справочники.СКМ_Настройки.СоздатьЭлемент();
		элТестоваяНастройка.Наименование		= стрНаименованиеТестового;
		элТестоваяНастройка.Адрес				= стрАдресТестового;
		элТестоваяНастройка.Активное			= Ложь;
		элТестоваяНастройка.Пароль				= стрПарольТестового;
		элТестоваяНастройка.Логин				= стрПользовательТестового;
		элТестоваяНастройка.АдресРесурса		= стрРесурсТестового;
		элТестоваяНастройка.АдресСервера		= стрСайтТестового;
		элТестоваяНастройка.Порт				= интПортТестового;
		элТестоваяНастройка.ПозИд				= стрАкцептантТестового;
		элТестоваяНастройка.ВидКартыЛояльности	= ссВидКарты.Ссылка;
		элТестоваяНастройка.Записать();
	КонецЕсли; 

Возврат Истина;
КонецФункции	// ПроверитьНаличиеОбъектов()


&НаСервере
Функция  ПолучитьНастройкиСервисаСКМ(Адрес, АдресСервера, АдресРесурса, Порт, Логин, Пароль, СоздаватьКартыЛояльности, ВидКартыЛояльности, ПозИд)  Экспорт
	
	_Запрос = Новый Запрос;
	_Запрос.Текст 	= "ВЫБРАТЬ
	              	  |	СКМ_Настройки.Адрес КАК Адрес,
	              	  |	СКМ_Настройки.АдресСервера КАК АдресСервера,
	              	  |	СКМ_Настройки.АдресРесурса КАК АдресРесурса,
	              	  |	СКМ_Настройки.Порт КАК Порт,
	              	  |	СКМ_Настройки.Логин КАК Логин,
	              	  |	СКМ_Настройки.Пароль КАК Пароль,
	              	  |	СКМ_Настройки.СоздаватьКартыЛояльности КАК СоздаватьКартыЛояльности,
	              	  |	СКМ_Настройки.ВидКартыЛояльности КАК ВидКартыЛояльности,
	              	  |	СКМ_Настройки.ПозИд КАК ПозИд
	              	  |ИЗ
	              	  |	Справочник.СКМ_Настройки КАК СКМ_Настройки
	              	  |ГДЕ
	              	  |	СКМ_Настройки.Активное = ИСТИНА";
	_АктивнаяЗапись = _Запрос.Выполнить().Выгрузить();
	
	Если _АктивнаяЗапись.Количество() > 0 Тогда
			
		Адрес 						= _АктивнаяЗапись[0].Адрес;
		АдресСервера 				= _АктивнаяЗапись[0].АдресСервера;
		АдресРесурса 				= _АктивнаяЗапись[0].АдресРесурса;
		Логин 						= _АктивнаяЗапись[0].Логин;
		Пароль 						= _АктивнаяЗапись[0].Пароль;
		СоздаватьКартыЛояльности 	= _АктивнаяЗапись[0].СоздаватьКартыЛояльности;
		ВидКартыЛояльности 			= _АктивнаяЗапись[0].ВидКартыЛояльности;
		ПозИд						= _АктивнаяЗапись[0].ПозИд;
		Порт 						= Число(_АктивнаяЗапись[0].Порт);

		Возврат Истина;
	КонецЕсли; 
	
	Возврат Ложь;
	
КонецФункции // ПолучитьНастройкиСервисаСКМ()


&НаСервере
Функция ПолучитьВидКартыЛояльностиСКМ() Экспорт
	
	_Запрос = Новый Запрос;
	_Запрос.Текст 	= "ВЫБРАТЬ
	              	  |	СКМ_Настройки.СоздаватьКартыЛояльности КАК СоздаватьКартыЛояльности,
	              	  |	СКМ_Настройки.ВидКартыЛояльности КАК ВидКартыЛояльности
	              	  |ИЗ
	              	  |	Справочник.СКМ_Настройки КАК СКМ_Настройки
	              	  |ГДЕ
	              	  |	СКМ_Настройки.Активное = ИСТИНА";
	_АктивнаяЗапись = _Запрос.Выполнить().Выгрузить();
	
	Если _АктивнаяЗапись.Количество() > 0 Тогда
		Если _АктивнаяЗапись[0].СоздаватьКартыЛояльности И Не ПустаяСтрока(_АктивнаяЗапись[0].ВидКартыЛояльности) Тогда
			Возврат _АктивнаяЗапись[0].ВидКартыЛояльности;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли; 
	
	Возврат Неопределено;
	
КонецФункции // ПолучитьВидКартыЛояльностиСКМ()


&НаСервере
Процедура ПроверитьКарту(__Штрихкод, __Статус, __Результат) Экспорт 
	
	Перем _Адрес, _АдресСервера, _АдресРесурса, _Порт, _Логин, _Пароль, _СоздаватьКартыЛояльности, _ВидКартыЛояльности, _Акцептант;
	//
	//	Получение настроек сервиса
	//
	СКМ_ОбщегоНазначенияВызовСервера.ПолучитьНастройкиСервисаСКМ(_Адрес, _АдресСервера, _АдресРесурса, _Порт, _Логин, _Пароль, _СоздаватьКартыЛояльности, _ВидКартыЛояльности, _Акцептант);

	//
	// Создание подключения
	//
	SSL = Новый ЗащищенноеСоединениеOpenSSL();
	Попытка
		_WSОпределение = Новый WSОпределения(_Адрес + "?wsdl",,,,,SSL);
	Исключение
		__Результат = "Ошибка связи с сервером проверки социальной карты москвича - адрес не подтвержден поставщиком услуг";
		Возврат;
	КонецПопытки;
	
	СвояФабрикаXDTO = _WSОпределение.ФабрикаXDTO;
	
	//
	//	Формирование запроса
	//
	стрСтрокаSOAP = СформироватьЗапросКСервисуСКМ(__Штрихкод, _Акцептант, _Логин, _Пароль);
	
	//
	// Описываем заголовки HTTP-запроса
	//
	Заголовки = Новый Соответствие;
	стрИмяФайлРезультата = ПолучитьИмяВременногоФайла("xml");
	
	Соединение 	= Новый HTTPСоединение(_АдресСервера, _Порт, _Логин, _Пароль); // Адрес должен быть без https://
	
	HTTPЗапрос = Новый HTTPЗапрос(_АдресРесурса, Заголовки);
	HTTPЗапрос.УстановитьТелоИзСтроки(стрСтрокаSOAP);
	
	Результат	= Соединение.ОтправитьДляОбработки(HTTPЗапрос, стрИмяФайлРезультата);
	
	стрРезультатПроверки 	= "";
	стрРасшифровкаОшибки	= "";
	Если Результат.КодСостояния	= 200 Тогда
		//
		//	Обработка успешного ответа сервера
		//
		_ЧтениеXML = Новый ЧтениеXML;
		_ЧтениеXML.ОткрытьФайл(стрИмяФайлРезультата);
		_ОбъектОтвет = СвояФабрикаXDTO.ПрочитатьXML(_ЧтениеXML);
		
		//
		//	Обработка статуса карты
		//
		Если _ОбъектОтвет.Body.processTransactionResponse.cardValid = "true" Тогда 
			__Статус = Истина;
		Иначе 
			__Статус = Ложь;
		КонецЕсли;
					
		//
		//	Обработка ответа
		//
		стрРезультатПроверки = стрРезультатПроверки + ?(_ОбъектОтвет.Body.processTransactionResponse.sex = "MALE", "Мужчина", "Женщина");
		стрРезультатПроверки = стрРезультатПроверки + ", полных лет " + _ОбъектОтвет.Body.processTransactionResponse.holderAge;

		//
		// Обработка списка привилегий
		//
		Если ТипЗнч(_ОбъектОтвет.Body.processTransactionResponse.privileges.privilege) = Тип("СписокXDTO") Тогда 
			
			//
			//	Несколько привилегий
			//
			стрРезультатПроверки = стрРезультатПроверки + Символы.ПС + "Доступные льготы:";
			Для Каждого _Запись из _ОбъектОтвет.Body.processTransactionResponse.privileges.privilege Цикл 
				стрРезультатПроверки = стрРезультатПроверки + Символы.ПС + Символы.Таб + _Запись.code + " - " + _Запись.name;
			КонецЦикла;
			
		ИначеЕсли ТипЗнч(_ОбъектОтвет.Body.processTransactionResponse.privileges.privilege) = Тип("ОбъектXDTO") Тогда 
			
			//
			//	Одна привилегий
			//
			стрРезультатПроверки = стрРезультатПроверки + Символы.ПС + "Доступные льготы:";
			стрРезультатПроверки = стрРезультатПроверки + Символы.ПС + Символы.Таб + _ОбъектОтвет.Body.processTransactionResponse.privileges.privilege.code 
			+ " - " + _ОбъектОтвет.Body.processTransactionResponse.privileges.privilege.name;
			
		КонецЕсли;
		
		_ЧтениеXML.Закрыть();
		УдалитьФайлы(стрИмяФайлРезультата);
		
	ИначеЕсли Результат.КодСостояния	= 500 Тогда
		
		//
		//	Обработка ошибки ответа
		//
		_ЧтениеXML = Новый ЧтениеXML;
		_ЧтениеXML.ОткрытьФайл(стрИмяФайлРезультата);
		_ОбъектОтвет = СвояФабрикаXDTO.ПрочитатьXML(_ЧтениеXML);
		
		стрРезультатПроверки = "Ошибка определения карты" + Символы.ПС + _ОбъектОтвет.Body.Fault.faultcode + " - " + _ОбъектОтвет.Body.Fault.faultstring;
		
		Попытка 
			стрРасшифровкаОшибки = Символы.ПС + _ОбъектОтвет.Body.Fault.detail.fault.code + " - " +	_ОбъектОтвет.Body.Fault.detail.fault.message;
			Если _ОбъектОтвет.Body.Fault.detail.fault.message <> _ОбъектОтвет.Body.Fault.detail.fault.comment Тогда 
				стрРасшифровкаОшибки = стрРасшифровкаОшибки + " (" + _ОбъектОтвет.Body.Fault.detail.fault.comment + ")";
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		стрРезультатПроверки = стрРезультатПроверки + стрРасшифровкаОшибки;
		_ЧтениеXML.Закрыть();
		УдалитьФайлы(стрИмяФайлРезультата);
		
	ИначеЕсли Результат.КодСостояния	= 403 Тогда
		
		//
		//	Обработка ошибки доступа
		//
		стрРезультатПроверки = "Ошибка связи с сервером проверки социальной карты москвича - адрес не подтвержден поставщиком услуг";
		
	Иначе 
		
		//
		//	Обработка прочих ситуаций
		//
		стрРезультатПроверки ="Ошибка связи с сервером проверки социальной карты москвича";
		
	КонецЕсли;
	
	__Результат = стрРезультатПроверки;
КонецПроцедуры


&НаСервере
Функция СформироватьЗапросКСервисуСКМ(Знач __Штрихкод, Знач _Акцептант, Знач _Логин, Знач _Пароль) Экспорт 
	
	Перем стрСерияКарты, стрСтрокаSOAP;
	
	стрСтрокаSOAP = "<soapenv:Envelope xmlns:soapenv=""http://schemas.xmlsoap.org/soap/envelope/"" xmlns:v1=""http://schemas.soccard.ru/tscws/TransactionService/v1"">
	|	<soapenv:Header>
	|		<wsse:Security soapenv:mustUnderstand=""1"" xmlns:wsse=""http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-wssecurity-secext-1.0.xsd"" xmlns:wsu=""http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-wssecurity-utility-1.0.xsd"">
	|			<wsse:UsernameToken>
	|				<wsse:Username>$$$login$$$</wsse:Username>
	|				<wsse:Password>$$$password$$$</wsse:Password>
	|			</wsse:UsernameToken>
	|		</wsse:Security>
	|	</soapenv:Header>
	|	<soapenv:Body>
	|		<v1:processTransactionRequest>
	|			<cardData>
	|				<cardSeries>$$$Series$$$</cardSeries>
	|				<cardNumber>$$$Number$$$</cardNumber>
	|			</cardData>
	|			<dicType>01</dicType>
	|			<transactionData>
	|				<posId>$$$posId$$$</posId>
	|				<transactionDate>$$$DDD$$$</transactionDate>
	|				<transactionAmount>0</transactionAmount>
	|				<serviceCode>00001</serviceCode>
	|			</transactionData>
	|		</v1:processTransactionRequest>
	|	</soapenv:Body>
	|</soapenv:Envelope>";
	
	стрСтрокаSOAP = СтрЗаменить(стрСтрокаSOAP, "$$$login$$$", _Логин);
	стрСтрокаSOAP = СтрЗаменить(стрСтрокаSOAP, "$$$password$$$", _Пароль);
	стрСтрокаSOAP = СтрЗаменить(стрСтрокаSOAP, "$$$posId$$$", _Акцептант);
	стрСтрокаSOAP = СтрЗаменить(стрСтрокаSOAP, "$$$DDD$$$", Формат(ТекущаяДата(), "ДФ=гггг-ММ-ддTчч:мм:сс"));
	стрСтрокаSOAP = СтрЗаменить(стрСтрокаSOAP, "$$$Number$$$", Лев(__Штрихкод, 19));
	стрСтрокаSOAP = СтрЗаменить(стрСтрокаSOAP, "$$$Series$$$", Сред(__Штрихкод, 21, 8));
	
	Возврат стрСтрокаSOAP;
	
КонецФункции


&НаСервере
Функция ПолучитьСтатусДисконтныхКарт() Экспорт

	Возврат Константы.ИспользоватьКартыЛояльности.Получить();	

КонецФункции // ПолучитьСтатусДисконтныхКарт()

&НаСервере
Процедура СоздатьКартуЛояльностиСКМПоШтрихкоду(Данные) Экспорт
	
	ссВидКартыЛояльности = ПолучитьВидКартыЛояльностиСКМ();
	Если ЗначениеЗаполнено(ссВидКартыЛояльности) Тогда
		ссКарта = Справочники.КартыЛояльности.НайтиПоРеквизиту("Штрихкод", Данные);
		Если Не ЗначениеЗаполнено(ссКарта) Тогда 
			ссКарта = Справочники.КартыЛояльности.СоздатьЭлемент();
			ссКарта.Наименование 	= Строка(ссВидКартыЛояльности) + " " + Данные;
			ссКарта.Штрихкод 		= Данные;
			ссКарта.Владелец		= ссВидКартыЛояльности;
			ссКарта.Статус			= Перечисления.СтатусыКартЛояльности.Действует;
			Попытка
				ссКарта.Записать();
			Исключение
			КонецПопытки;
		Иначе 
			ссОбъект = ссКарта.ПолучитьОбъект();
			Если Не ссОбъект.Статус = Перечисления.СтатусыКартЛояльности.Действует Тогда
				ссОбъект.ОбменДанными.Загрузка = Истина;
				ссОбъект.Статус = Перечисления.СтатусыКартЛояльности.Действует;
				Попытка
					ссКарта.Записать();
				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
			
КонецПроцедуры
