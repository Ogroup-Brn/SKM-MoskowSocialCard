
&НаКлиенте
Процедура ПроверитьКарту(Команда)
	Перем флСтатус, стрШтрихкод;
	стрПрефиксСКМ = "964390";
	
	//
	//	Инициализация элементов формы
	//
	Элементы.СоздатьКарту.Доступность				= Ложь;
	Элементы.СтатусКартыНеДействующая.Видимость	= Ложь;
	Элементы.СтатусКартыДействующая.Видимость		= Ложь;
	Элементы.РезультатПроверки.Видимость			= Ложь;
	Элементы.РезультатПроверки.ЦветТекста			= Новый Цвет(0, 0, 0);
	ТекущийЭлемент = Элементы.НомерКарты; 
	ЭтаФорма.Высота = 0;
	ЭтаФорма.ОбновитьОтображениеДанных();

	Если СтрДлина(СокрП(НомерКарты)) = 16 И СтрДлина(СерияКарты) = 8 Тогда
		ШтрихкодКарты = стрПрефиксСКМ + СтрЗаменить(НомерКарты, " ", "") + " " 
		+ Сред(СерияКарты, 4, 2) + Лев(СерияКарты, 2) + Сред(СерияКарты, 7, 2) + "77";
	КонецЕсли;
	
	//
	//	Проверка карты
	//
	Если ЗначениеЗаполнено(ШтрихкодКарты) Тогда 
		СКМ_ОбщегоНазначенияВызовСервера.ПроверитьКарту(ШтрихкодКарты, флСтатус, ЭтаФорма.Элементы.РезультатПроверки.Заголовок);
	КонецЕсли;
	
	//
	//	Отображение результатов
	//
	Если флСтатус <> Неопределено Тогда 
		
		Элементы.СтатусКартыДействующая.Видимость		= флСтатус;
		Элементы.СоздатьКарту.Доступность				= флСтатус;
		
		Если Не флСтатус Тогда
			Элементы.СтатусКартыНеДействующая.Видимость	= Истина;
			Элементы.РезультатПроверки.ЦветТекста		= Новый Цвет(160, 160, 160);
		КонецЕсли; 
		
	КонецЕсли;
	
	Элементы.РезультатПроверки.Видимость	= Истина;
	ЭтаФорма.ОбновитьОтображениеДанных();
	
КонецПроцедуры


&НаКлиенте
Процедура СоздатьКартуЛояльности(Команда)
	
	фСозданиеКарты = ПолучитьФорму("Справочник.КартыЛояльности.ФормаОбъекта");
	фСозданиеКарты.Объект.Штрихкод 	= ШтрихкодКарты;
	фСозданиеКарты.Объект.Владелец	= ВидКартыЛояльности;
	фСозданиеКарты.Объект.Наименование = Строка(ВидКартыЛояльности) + " " + ШтрихкодКарты;
	фСозданиеКарты.Открыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СоздаватьКартыЛояльности = СКМ_ОбщегоНазначенияВызовСервера.ПолучитьСтатусДисконтныхКарт();
	Если СоздаватьКартыЛояльности Тогда 
		ВидКартыЛояльности	= СКМ_ОбщегоНазначенияВызовСервера.ПолучитьВидКартыЛояльностиСКМ();
	КонецЕсли;
		
КонецПроцедуры
